class K2k
types

values
-- TODO Define values here
instance variables
				private stores : set of Store := {};
				inv not exists l1, l2 in set stores & 
						l1 <> l2 and l1.getID() = l2.getID(); 
				
				private range1 : Product`Range := mk_Product`Range(10, 12, 'Y');
				private range2 : Product`Range := mk_Product`Range(5, 8, 'M');
				private range3 : Product`Range := mk_Product`Range(8, 9, 'Y');
				private price1 : Product`Price := mk_Product`Price(7);
				private price2 : Product`Price := mk_Product`Price(4);
				private price3 : Product`Price := mk_Product`Price(15);
				private product1 : Product := new Product(<toy>, range1, "zippo", "carro", price1, 'M');
				private product2 : Product := new Product(<toy>, range2, "zippo", "boneca", price2, 'F');
				private product3 : Product := new Product(<clothing>, range3, "zippo", "casaco", price3, 'M');
				private inventory : set of Product := {product1, product2, product3};
				private trans: seq of Transaction := [];
				private store1 : Store := new Store(1, "Porto", inventory, trans, 100.0, 10);
				private store2 : Store := new Store(2, "Vila Real", inventory, trans, 150.0, 15);
				private store3 : Store := new Store(3, "Braga", inventory, trans, 80.0, 20);

				
operations
			public static main: () ==> ()
			main() ==
			(
				new K2k().init();
			);
			
			
			public init : () ==> ()
			init() ==
			(
				dcl client1 : Client := new Client("Ze");
				dcl trans1: Transaction ;
				dcl trans2: Transaction ;
				--Product(stringCat, stringBrand, stringDetail, value, genderChar) 				
				
				--Store(string, products, trans, cut)
				stores := {store1, store2, store3};
				--Transaction(cli, str, prdt, t, val)
				trans1 := new Transaction(client1, store1, {product1}, <buy>);
				trans2 := new Transaction(client1, store2, {product2}, <buy>);
				store2.addTransaction(trans1);
				store2.addTransaction(trans2);
				store1.addTransaction(trans1);
			);
			
			--returns the store(s) with the most total transactions
			public mostSales : () ==> set of Store
			mostSales() ==
			(
				dcl max : int := 0;
				dcl maxStores: set of Store := {};
				for all store in set stores do (
					if numTransactions(store) >= max then(
						if numTransactions(store) > max then(
							max := numTransactions(store);
							maxStores := {};
						);
						maxStores := {store} union maxStores;
					);
				);
				return maxStores;
			);
			
			--returns the store(s) with the most <buy> transactions
			public mostBuys : () ==> set of Store
			mostBuys() ==
			(
				dcl max : int := 0;
				dcl maxStores: set of Store := {};
				for all store in set stores do (
					if numBuys(store) >= max then(
						if numBuys(store) > max then(
							max := numBuys(store);
							maxStores := {};
						);
						maxStores := {store} union maxStores;
					);
				);
				return maxStores;
			);
			
			--returns the store(s) with the most <sell> transactions
			public mostSells : () ==> set of Store
			mostSells() ==
			(
				dcl max : int := 0;
				dcl maxStores: set of Store := {};
				for all store in set stores do (
					if numSells(store) >= max then(
						if numSells(store) > max then(
							max := numSells(store);
							maxStores := {};
						);
						maxStores := {store} union maxStores;
					);
				);
				return maxStores;
			);
			
			--returns the store(s) with the highest balance
			public highestBalance : () ==> set of Store
			highestBalance() ==
			(
				dcl max : real := 0;
				dcl maxStores: set of Store := {};
				for all store in set stores do (
					if store.getBalance() >= max then(
						if store.getBalance() > max then(
							max := store.getBalance();
							maxStores := {};
						);
						maxStores := {store} union maxStores;
					);
				);
				return maxStores;
			);
			
			--returns the number of transactions of a store
			public numTransactions : Store ==> int
			numTransactions(store) ==
			(
				return len store.getTransactions();
			);
			
			--returns stores ordered by balance (bubbleSort)
			public balanceList : () ==> seq of Store
			balanceList() ==
			(
					dcl sortedStores : seq of Store := [];
					
					for all store in set stores do(
						sortedStores := sortedStores ^ [store];
					);
					
					for i = len sortedStores to 1 by -1 do
						for j = 1 to i-1 do
							if sortedStores(j).getBalance() < sortedStores(j+1).getBalance()
								then (dcl temp:Store := sortedStores(j);
								sortedStores(j) := sortedStores(j+1);
								sortedStores(j+1) := temp
							);
					return sortedStores
				);
				
			--returns the number of <buy> of a store
			public numBuys : Store ==> int
			numBuys(store) ==
			(
				dcl num : int := 0;
				dcl transactions : seq of Transaction := store.getTransactions();
				
				for transaction in transactions do (
					if transaction.getType() = <buy> then
						num := num + 1;
				
				);
				return num;
			);
			
			--returns the number of <sell> of a store
			public numSells : Store ==> int
			numSells(store) ==
			(
				dcl num : int := 0;
				dcl transactions : seq of Transaction := store.getTransactions();
				
				for transaction in transactions do (
					if transaction.getType() = <sell> then
						num := num + 1;
				);
				return num;
			);
			
			--returns number of <buy> on entire chain
			public totalBuys : () ==> int
			totalBuys() ==
			(
				dcl num : int := 0;
				for all store in set stores do (
					num := num + numBuys(store);
				);
				return num;
			);
			
			--returns number of <sell> on entire chain
			public totalSells : () ==> int
			totalSells() ==
			(
				dcl num : int := 0;
				for all store in set stores do (
					num := num + numSells(store);
				);
				return num;
			);
			
			--returns transactions of a store
			public getTransactions : Store ==> seq of Transaction
			getTransactions(store) ==
			(
				return store.getTransactions();
			);
			
			--returns the average <buy> price of a store
			public avgBuy : Store ==> real
			avgBuy(store) ==
			(
				dcl avg : real := 0;
				dcl num : int := 0;
				dcl transactions : seq of Transaction := store.getTransactions();
				for transaction in transactions do (
					if transaction.getType() = <buy> then(
						num := num + 1;
						avg := avg + transaction.getValue();
					)
				);
				avg := avg / num;
				return avg;
			);
			
			--returns the average <sell> price of a store
			public avgSell : Store ==> real
			avgSell(store) ==
			(
				dcl avg : real := 0;
				dcl num : int := 0;
				dcl transactions : seq of Transaction := store.getTransactions();
				for transaction in transactions do (
					if transaction.getType() = <sell> then(
						num := num + 1;
						avg := avg + transaction.getValue();
					)
				
				);
				avg := avg / num;
				return avg;
			);
			
			public productSearch: Product`Category * Product`Range * Product`String * char ==> map Store to Product
			productSearch(cat, age, brand, gender) == (
			
			dcl results : map Store to Product:= {|->};

			for all store in set stores do(
				for all prdct in set store.getInventory() do(
					if(prdct.getCategory() = cat 
						and prdct.getAgeRange() = age
						and prdct.getBrand() = brand 
						and (prdct.getGender() = gender or prdct.getGender() = 'N'))
					then(
						results := results munion {store |-> prdct}; 
					)
				)
			);
			return results;
	)
	pre (cat <> nil
			and age <> mk_Product`Range(0,1,' ')
			and brand <> " "
			and gender <> ' ')
	;
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end K2k